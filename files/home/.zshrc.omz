# vim: ft=zsh
#———— oh-my-zsh ——————————————————————————————————
# This file wraps Oh-My-ZSH by providing a consistent installation location
# and a convenient function to add plugins as necessary. The more plugins the
# longer it takes to load ZSH.

# Set these to 1 to see more output
export OMZ_DEBUG=${OMZ_DEBUG:-"0"}
export OMZ_VERBOSE=${OMZ_VERBOSE:-"0"}

function is-tty() {
  [[ -t 1 ]]
}

export ZSH="${HOME}/.oh-my-zsh"

if [[ -d ${ZSH} && -f ${ZSH}/oh-my-zsh.sh ]]; then
  ((OMZ_VERBOSE)) && echo "Updating Oh My Zsh..."
  ( cd "${ZSH}" && git pull --rebase >/dev/null )
else
  ((OMZ_VERBOSE)) && echo "Installing Oh My Zsh..."
  bash -c "$(curl -fsSL https://raw.github.com/ohmyzsh/ohmyzsh/master/tools/install.sh)"
  if [[ -f ~/.zshrc.omz ]] ; then
    [[ -f ~/.zshrc ]] && mv ~/.zshrc ~/.zshrc.bak
    [[ -f ~/.zshrc.pre-oh-my-zsh ]] && mv ~/.zshrc.pre-oh-my-zsh ~/.zshrc
    grep -q zshrc.omz ~/.zshrc || echo "source ~/.zshrc.omz" >> ~/.zshrc
  fi
fi

# List of Plug-Ins feor Oh-My-Zsh
#
# 1password         chucknorris        ember-cli         git-prompt                jruby             mongo-atlas            pow           sfffe          tmux-cssh
# adb               cloudfoundry       emoji             gitfast                   jsontools         mongocli               powder        shell-proxy    tmuxinator
# ag                codeclimate        emoji-clock       github                    juju              mosh                   powify        shrink-path    toolbox
# alias-finder      coffee             emotty            gitignore                 jump              multipass              pre-commit    sigstore       torrent
# aliases           colemak            encode64          glassfish                 kate              mvn                    profiles      singlechar     transfer
# ansible           colored-man-pages  extract           globalias                 keychain          mysql-macports         pyenv         skaffold       tugboat
# ant               colorize           fabric            gnu-utils                 kitchen           n98-magerun            pylint        spring         ubuntu
# apache2-macports  command-not-found  fancy-ctrl-z      golang                    kn                nanoc                  python        sprunge        ufw
# arcanist          common-aliases     fasd              gpg-agent                 knife             nats                   qrcode        ssh-agent      universalarchive
# archlinux         compleat           fastfile          gradle                    knife_ssh         ng                     rails         stack          urltools
# argocd            composer           fbterm            grails                    kops              nmap                   rake          starship       vagrant
# asdf              copybuffer         fd                grc                       kube-ps1          node                   rake-fast     sublime        vagrant-prompt
# autoenv           copyfile           fig               grunt                     kubectl           nodenv                 rand-quote    sublime-merge  vault
# autojump          copypath           firewalld         gulp                      kubectx           nomad                  rbenv         sudo           vi-mode
# autopep8          cp                 flutter           hanami                    lando             npm                    rbfu          supervisor     vim-interaction
# aws               cpanm              fluxcd            hasura                    laravel           nvm                    rbw           suse           virtualenv
# azure             dash               fnm               helm                      laravel4          oc                     react-native  svcat          virtualenvwrapper
# battery           dbt                forklift          heroku                    laravel5          octozen                rebar         svn            volta
# bazel             debian             fossil            heroku-alias              last-working-dir  operator-sdk           redis-cli     svn-fast-info  vscode
# bbedit            deno               frontend-search   history                   lein              otp                    repo          swiftpm        vundle
# bedtools          dircycle           fzf               history-substring-search  lighthouse        pass                   ripgrep       symfony        wakeonlan
# bgnotify          direnv             gas               hitchhiker                lol               paver                  ros           symfony2       watson
# bower             dirhistory         gatsby            hitokoto                  lpass             pep8                   rsync         systemadmin    wd
# branch            dirpersist         gcloud            homestead                 lxd               per-directory-history  ruby          systemd        web-search
# brew              dnf                geeknote          httpie                    macos             percol                 rust          taskwarrior    wp-cli
# bridgetown        dnote              gem               invoke                    macports          perl                   rvm           term_tab       xcode
# bundler           docker             genpass           ionic                     magic-enter       perms                  safe-paste    terminitor     yarn
# cabal             docker-compose     gh                ipfs                      man               phing                  salt          terraform      yii
# cake              docker-machine     git               isodate                   marked2           pip                    samtools      textastic      yii2
# cakephp3          doctl              git-auto-fetch    istioctl                  marktext          pipenv                 sbt           textmate       yum
# capistrano        dotenv             git-escape-magic  iterm2                    mercurial         pj                     scala         thefuck        z
# cask              dotnet             git-extras        jake-node                 meteor            please                 scd           themes         zbell
# catimg            droplr             git-flow          jenv                      microk8s          pm2                    screen        thor           zeus
# celery            drush              git-flow-avh      jfrog                     minikube          pod                    scw           tig            zoxide
# charm             eecms              git-hubflow       jhbuild                   mix               poetry                 sdk           timer          zsh-interactive-cd
# chruby            emacs              git-lfs           jira                      mix-fast          postgres               sfdx          tmux           zsh-navigation-tools

declare -g theme
theme=
if [[ -f ~/.zsh_last_theme ]]; then
  theme="$(cat ~/.zsh_last_theme  | tr -d '\n')"
elif [[ -f ~/.zsh_favlist ]]; then
  theme="$(shuf -n 1 ~/.zsh_favlist)"
  echo "${theme}" > ~/.zsh_last_theme
else
  theme="agnoster"
fi

ZSH_THEME="${theme:-"agnoster"}"

((OMZ_VERBOSE)) && echo "Oh My ZSH Theme is ${ZSH_THEME}"

CASE_SENSITIVE="true"
DISABLE_UNTRACKED_FILES_DIRTY="true"
HIST_STAMPS="yyyy-mm-dd"

# HYPHEN_INSENSITIVE="true"
# DISABLE_AUTO_UPDATE="true"
# DISABLE_UPDATE_PROMPT="true"
# DISABLE_MAGIC_FUNCTIONS="true"
# DISABLE_LS_COLORS="true"
# DISABLE_AUTO_TITLE="true"
# ENABLE_CORRECTION="true"
# COMPLETION_WAITING_DOTS="true"
# ZSH_CUSTOM=/path/to/new-custom-folder

alias edit_zshrc="vim ~/.zshrc"
alias edit_ohmyzsh="vim ~/.zshrc.omz"

declare -a plugins
export plugins=(
  z wd themes aliases
)

declare plugin_info=$(mktemp)
rm -f ${plugin_info}

function omz-add-plugins() {
  for plugin in "$@"; do
    if [[ -d $ZSH/plugins/${plugin} ]]; then
      ((OMZ_VERBOSE)) && {
        omz plugin info ${plugin} >> $plugin_info
      }
      ((OMZ_DEBUG)) && echo "loading plugin ${plugin}"
      plugins+=(${plugin})
    else
      echo "Plugin [${plugin}] is not installed under $ZSH/plugins folder." >&2
    fi
  done
}

[[ -s $plugin_info ]] && cat $plugin_info

alias zap="omz-add-plugins"

# Likely used for HealthSherpa [[ "$(uname -s | tr -d '\n')" =~ Darwin ]] && zap osx brew
gnu_installed=true
for cmd in gmkdir gawk gsed; do
  command -v ${cmd} >/dev/null || {
    gnu_installed=false
    break
  }
done

((OMZ_VERBOSE)) && echo "HINT: set OMZ_DEBUG=1 to see plugins as they load."

${gnu_installed}                           && zap gnu-utils

command -v brew >/dev/null                 && zap brew cask
command -v git >/dev/null                  && zap gh gitfast git-extras git-lfs git-prompt gitfast github gitignore
command -v ag >/dev/null                   && zap ag
command -v rg >/dev/null                   && zap ripgrep
command -v fzf >/dev/null                  && zap fzf
command -v direnv >/dev/null               && zap direnv
command -v heroku >/dev/null               && zap heroku
command -v aws >/dev/null                  && zap aws
command -v gh >/dev/null                   && zap github
command -v grc >/dev/null && 
  [[ -d ~/.grc ]]                          && zap grc

[[ -d ${HOME}/.rbenv ]]                    && zap rbenv ruby rake rails bundler rake-fast

[[ -d ${HOME}/.nvm || -d ${HOME}/.volta \
  || $(command -v node >/dev/null) ]]      && zap node gulp volta

ps -ef | grep -i -q '[d]ocker'             && zap docker docker-compose

# Additional
command -v go >/dev/null                   && zap golang
[[ -d ${HOME}/.pyenv ]]                    && zap pip python

((OMZ_VERBOSE)) && {
  echo "The following OMZ plugins are installed:"
  echo "${plugins[@]}"
}


source $ZSH/oh-my-zsh.sh 2>/dev/null


